Package bemtevi;

Helpers
	digito = ['0' .. '9'];
	caractere_maiusculo = ['A' .. 'Z'];
	caractere_minusculo = ['a' .. 'z'];
	alfabeto = (caractere_minusculo | caractere_maiusculo);
	espaco = ' ';
	tab = '	';
	hifen = '_';
	ponto = '.';
	virgula = ',';
	numero_inteiro = digito+;
	numero_virgula = digito+ virgula digito+;
	numero_cientifico = digito ('e' | 'E') ('-' | '+') digito;
	abre_chaves = '{';
	fecha_chaves = '}';
	rc = 13;
	nl = 10;

Tokens
	primitivo = 'Bool' | 'Numero' | 'Texto';
	numero = numero_inteiro | numero_virgula | numero_cientifico;
	verdade = 'verdade';
	falso = 'falso';
	molde = 'molde';
	se = 'se';
	entao = 'entao';
	senao = 'senao';
	passa = 'passa';
	por = 'por';
	ponto_virgula = ';';
	contem = 'contem';
	constante = 'constante';
	funcao = 'funcao';
	objeto = 'objeto';
	fim = 'fim';
	separador = '|';
	dois_pontos = ':';
	ponto_final = '.';
	abre_parenteses = '(';
	fecha_parenteses = ')';
	abre_colchete = '[';
	fecha_colchete = ']';
	abre_aspas = '"';
	multiplicacao = '*';
	soma = '+';
	subtracao = '-';
	divisao = '/';
	modulo = '%';
	igual_igual = '==';
	igual = '=';
	menor_que = '<';
	menor_igual = '<=';
	exclamacao = '!';
	e = 'e';
	ou = 'ou';
	entrada = '>>';
	vazio = (espaco | rc | nl | tab)+;
	comentario_linha = '--' ([0x00 .. 0x09] | [0x0b .. 0x0c] | [0x0e .. 0xff])*;
	id_molde = caractere_maiusculo (caractere_minusculo | hifen | caractere_maiusculo)*;
	id = caractere_minusculo (hifen | caractere_minusculo | caractere_maiusculo)*;
	texto = (caractere_maiusculo | caractere_minusculo)+;
	comentario_bloco = abre_chaves ([0x00 .. 0x7c] | [0x7e .. 0xff])* fecha_chaves;
	string = '"' ([0x00 .. 0x21] | [0x23 .. 0xff])* '"';

Ignored Tokens
 	vazio, comentario_linha, comentario_bloco;

Productions
	inicio {-> inicio} = 
		programa {-> New inicio(programa)};
	
	programa {-> programa} = 
	    familia [esquerda]:def_molde [direita]:def_molde* {-> New programa(familia, esquerda.def_molde, [direita.def_molde])};

	familia {-> familia} = 
		relacao familia_fecho* ponto_final {-> New familia(relacao, [familia_fecho.relacao])}
		|{vazio} {-> New familia.vazio()};

	familia_fecho {-> relacao} = 
		ponto_virgula relacao {-> relacao.relacao};

	relacao {-> relacao} = 
 		molde [esquerdo]:id_molde se passa por [direito]:id_molde {-> New relacao(esquerdo, direito)};

	def_molde {-> def_molde} = 
 	    molde id_molde contem atributos* dec_funcao* fim {-> New def_molde(id_molde, [atributos.bloco_fecho], [dec_funcao])};

	atributos {-> bloco_fecho} = 
 	    {objeto} dec_obj {-> dec_obj.bloco_fecho}
     	|{constante} dec_cons {-> dec_cons.bloco_fecho};

	tipo {-> tipo} = 
		{primitivo} primitivo {-> New tipo.primitivo(primitivo)}
		|{molde} id_molde {-> New tipo.molde(id_molde)};

	dec_funcao {-> dec_funcao} =
		{func1} entrada? funcao dois_pontos tipo id abre_parenteses parametros fecha_parenteses blloco {-> New dec_funcao.func1(tipo, id, parametros, blloco.exp) }
		|{func2} entrada? funcao dois_pontos tipo id abre_parenteses parametros fecha_parenteses ponto_virgula {-> New dec_funcao.func2(tipo, id, parametros) };

	parametros {-> parametros} = 
 	    parametro parametro_fecho* {-> New parametros(parametro, [parametro_fecho.parametro])}
 	    |{vazio} {-> New parametros.vazio()};

	parametro_fecho {-> parametro} = 
		separador parametro {-> parametro.parametro};

	parametro {-> parametro}= 
		{id} tipo id {-> New parametro.id(tipo, id)}
		|{assinatura} assinatura {-> New parametro.assinatura(assinatura)};

	assinatura {->assinatura} = 
		tipo id abre_parenteses parametros_assinatura fecha_parenteses {-> New assinatura(tipo, id, parametros_assinatura)};

	parametros_assinatura {-> parametros_assinatura} = 
		parametro_assinatura parametros_assinatura_fecho* {-> New parametros_assinatura(parametro_assinatura, [parametros_assinatura_fecho.parametro_assinatura])}
		|{vazio} {-> New parametros_assinatura.vazio()};

	parametros_assinatura_fecho {-> parametro_assinatura}= 
		separador parametro_assinatura {-> parametro_assinatura.parametro_assinatura};
	
	parametro_assinatura {-> parametro_assinatura} = 
		{tipo} tipo {-> New parametro_assinatura.tipo(tipo)}
		|{assinatura} assinatura {-> New parametro_assinatura.assinatura(assinatura)};
	
	blloco {-> exp} = 
		abre_colchete bloco_fecho* exp fecha_colchete {-> New exp.bloco([bloco_fecho], exp)};

	bloco_fecho {-> bloco_fecho} = 
		{constante} dec_cons {-> dec_cons.bloco_fecho}
		|{objeto} dec_obj {-> dec_obj.bloco_fecho}
		|{definicao} definicao {-> definicao.bloco_fecho};
	
	dec_obj {-> bloco_fecho} = 
		objeto dois_pontos tipo_molde id ponto_virgula {-> New bloco_fecho.objeto(tipo_molde, id)};
	
	dec_cons {-> bloco_fecho} = 
		constante dois_pontos tipo_primitivo id ponto_virgula {-> New bloco_fecho.constante(tipo_primitivo, id)};

	tipo_primitivo {-> tipo_primitivo} = 
		primitivo {-> New tipo_primitivo(primitivo)};

	tipo_molde {-> tipo_molde} = 
		id_molde {-> New tipo_molde(id_molde)};

	definicao {-> bloco_fecho} = 
		id_ou_attr igual exp ponto_virgula {-> New bloco_fecho.definicao(id_ou_attr, exp)};

	id_ou_attr {-> id_ou_attr} = 
		{id} id {-> New id_ou_attr.id(id)}
		|{atributo} id id_fecho+ {-> New id_ou_attr.atributo(id, [id_fecho])};

	id_fecho {-> id_fecho} = 
		ponto_final id {-> New id_fecho(id)};
	
	chamada {-> exp} = 
		id id_fecho* abre_parenteses lista_exp fecha_parenteses {-> New exp.chamada(id, [id_fecho], lista_exp)};

	lista_exp {-> lista_exp} = 
		exp l_exp_fecho* {-> New lista_exp(exp, [l_exp_fecho.exp])}
		|{vazio} {-> New lista_exp.vazio()};

	l_exp_fecho {-> exp} = 
		separador exp {-> exp.exp};

	instancia {-> exp} = 
		id_molde abre_colchete lista_init fecha_colchete {-> New exp.instancia(id_molde, lista_init)};

	lista_init {-> lista_init} = 
		init_objeto l_init_fecho* {-> New lista_init(init_objeto, [l_init_fecho.init_objeto])}
		|{vazio} {-> New lista_init.vazio()};

	l_init_fecho {-> init_objeto} = 
		separador init_objeto {-> init_objeto.init_objeto};

	init_objeto {-> init_objeto} = 
		objeto ponto_final id id_fecho* igual exp {-> New init_objeto(id, [id_fecho], exp)};

	exp {-> exp} = 
		{ou} ou_logico {-> ou_logico.exp}
		|{chamada} chamada {-> chamada.exp}
		|{bloco} blloco {-> blloco.exp}
		|{instancia} instancia {-> instancia.exp}
		|{ternario} se abre_parenteses [esquerdo]:exp fecha_parenteses entao [centro]:exp senao [direito]: exp {-> New exp.ternario(esquerdo.exp, centro.exp, direito.exp)};
	
	ou_logico {-> exp} = 
		{ou} ou_logico ou e_logico {-> New exp.ou(ou_logico.exp, e_logico.exp)}
		|{e} e_logico {-> e_logico.exp};
	
	e_logico {-> exp} = 
		{comparacao} comparacao {-> comparacao.exp}
		|{e} e_logico e comparacao {-> New exp.e(e_logico.exp, comparacao.exp)};
	
	comparacao {-> exp} = 
		{relacional} relacional {-> relacional.exp}
		|{comparacao} comparacao igual_igual relacional {-> New exp.comparacao(comparacao.exp, relacional.exp)};
	
	relacional {-> exp} = 
		{aritmeticos1} aritmeticos1 {-> aritmeticos1.exp}
		|{menor} relacional menor_que aritmeticos1 {-> New exp.menor_que(relacional.exp, aritmeticos1.exp)}
		|{menor_igual} relacional menor_igual aritmeticos1 {-> New exp.menor_igual(relacional.exp, aritmeticos1.exp)};
	
	aritmeticos1 {-> exp} = 
		{aritmeticos2} aritmeticos2 {-> aritmeticos2.exp}
		|{soma} aritmeticos1 soma aritmeticos2 {-> New exp.soma(aritmeticos1.exp, aritmeticos2.exp)}
		|{subtracao} aritmeticos1 subtracao aritmeticos2 {-> New exp.subtracao(aritmeticos1.exp, aritmeticos2.exp)};
	
	aritmeticos2 {-> exp} = 
		{aritmeticos3} aritmeticos3 {-> aritmeticos3.exp}
		|{multiplicacao} aritmeticos2 multiplicacao aritmeticos3 {-> New exp.multiplicacao(aritmeticos2.exp, aritmeticos3.exp)}
		|{divisao} aritmeticos2 divisao aritmeticos3 {-> New exp.divisao(aritmeticos2.exp, aritmeticos3.exp)}
		|{modulo} aritmeticos2 modulo aritmeticos3 {-> New exp.modulo(aritmeticos2.exp, aritmeticos3.exp)};
	
	aritmeticos3 {-> exp} = 
		{termo} termo {-> termo.exp}
		|{negacao} exclamacao termo {-> New exp.negacao(termo.exp)}
		|{negativo} subtracao termo {-> New exp.negativo(termo.exp)};
	
	termo {-> exp} =  
		{numero} numero {-> New exp.numero(numero)}
		|{string} string {-> New exp.string(string)}
		|{id} id {-> New exp.id(id)}
		|{texto} texto {-> New exp.texto(texto)}
		|{verdade} verdade {-> New exp.verdade(verdade)}
		|{falso} falso {-> New exp.falso(falso)}
		|{parenteses} abre_parenteses exp fecha_parenteses {-> exp.exp};

Abstract Syntax Tree
	inicio = 
		programa;
	
	programa = 
	    familia [esquerda]:def_molde [direita]:def_molde*;
	
	familia = 
		[esquerdo]:relacao [direito]:relacao*
		|{vazio};

	relacao = 
		[esquerdo]:id_molde [direito]:id_molde;
	
	def_molde = 
 	    id_molde bloco_fecho* dec_funcao*;
	
	tipo = 
		{primitivo} primitivo
		|{molde} id_molde;

	dec_funcao =
		{func1} tipo id parametros exp
		|{func2} tipo id parametros;

	parametros = 
 	    [esquerdo]:parametro [direito]:parametro*
 	    |{vazio};

	parametro = 
		{id} tipo id
		|{assinatura} assinatura;

	assinatura = 
		tipo id parametros_assinatura;

	parametros_assinatura = 
		[esquerdo]:parametro_assinatura [direito]:parametro_assinatura*
		|{vazio};
	
	parametro_assinatura = 
		{tipo} tipo
		|{assinatura} assinatura;
	
	bloco_fecho = 
		{constante} tipo_primitivo id
		|{objeto} tipo_molde id
		|{definicao} id_ou_attr exp;

	tipo_primitivo = 
		primitivo;

	tipo_molde = 
		id_molde;

	id_ou_attr = 
		{id} id
		|{atributo} id id_fecho+;
	
	id_fecho = 
		id;
	
	lista_exp = 
		[esquerdo]:exp [direito]:exp*
		|{vazio};
	
	lista_init = 
		[esquerdo]:init_objeto [direito]:init_objeto*
		|{vazio};
	
	init_objeto = 
		id id_fecho* exp;

	exp = 
		{ternario} [esquerdo]:exp [central]:exp [direito]:exp
		|{ou} [esquerdo]:exp [direito]:exp
		|{e} [esquerdo]:exp [direito]:exp
		|{comparacao} [esquerdo]:exp [direito]:exp
		|{menor_que} [esquerdo]:exp [direito]:exp
		|{menor_igual} [esquerdo]:exp [direito]:exp
		|{soma} [esquerdo]:exp [direito]:exp
		|{subtracao} [esquerdo]:exp [direito]:exp
		|{multiplicacao} [esquerdo]:exp [direito]:exp
		|{divisao} [esquerdo]:exp [direito]:exp
		|{modulo} [esquerdo]:exp [direito]:exp
		|{negacao} exp
		|{negativo} exp
		|{chamada} id id_fecho* lista_exp
		|{instancia} id_molde lista_init
		|{bloco} bloco_fecho* exp
		|{numero} numero
		|{id} id
		|{string} string
		|{texto} texto
		|{verdade} verdade
		|{falso} falso;
